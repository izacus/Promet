buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.google.gms:google-services:4.3.2'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "io.realm:realm-gradle-plugin:5.15.1"
        classpath 'com.google.firebase:perf-plugin:1.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.firebase-perf'

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.fabric.io/public' }
}

ext {
    androidxLibraryVersion = '1.0.0'
    retrofitVersion = '2.6.1'
    daggerVersion = '2.19'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "si.virag.promet"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 3000
        versionName "1.9.0"
        resConfigs "en", "sl"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    productFlavors {}
}

dependencies {
    // Support
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core:1.1.0"
    implementation "androidx.annotation:annotation:$androidxLibraryVersion"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.cardview:cardview:$androidxLibraryVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxLibraryVersion"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.viewpager:viewpager:$androidxLibraryVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidxLibraryVersion"
    implementation "com.google.android.material:material:$androidxLibraryVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.14.3'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.3'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex:rxandroid:1.2.1'

    // Play Services
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.google.firebase:firebase-perf:19.0.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0@aar'

    // WorkManager
    implementation 'androidx.work:work-runtime:2.2.0'
    implementation 'androidx.work:work-rxjava2:2.2.0'
    implementation 'androidx.work:work-gcm:2.2.0'

    implementation 'com.nispok:snackbar:2.11.0'
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'
    implementation 'com.github.franmontiel:LocaleChanger:0.9.2'
    implementation('com.github.afollestad.material-dialogs:core:0.9.6.0@aar') {
        transitive = true
    }
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    implementation 'de.greenrobot:eventbus:2.4.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    // Android 6 permissions
    implementation('com.github.tbruyelle:rxpermissions:0.10.2') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'